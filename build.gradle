plugins {
    id 'fabric-loom' version '1.9+'
    id 'maven-publish'
}

group = "com.github.hugman"

base {
    archivesName = project.mod_id
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    compileOnly "com.google.code.findbugs:jsr305:3.0.2"
}

loom {
    accessWidenerPath = file("src/main/resources/universal_ores.accesswidener")

    splitEnvironmentSourceSets()

    mods {
        "universal_ores" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }

}

fabricApi {
    configureDataGeneration {
        modId = "universal_ores-datagen"
        createSourceSet = true
        client = true
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 21
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}"}
    }
}

// for JitPack
configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}